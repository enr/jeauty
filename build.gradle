
plugins {
    id 'java'
    id 'eclipse'
    id 'jacoco'
    id 'project-report'
    id 'build-dashboard'
    id 'application'
    id 'findbugs'
    id 'pmd'
    id 'com.github.johnrengelman.shadow' version '1.2.2'
    id 'com.github.hierynomus.license' version '0.11.0'
}

version = '0.0.1'

repositories {
    jcenter()
    mavenCentral()
}

ext {
    mainClass = 'com.atoito.jeauty.App'
}

mainClassName = mainClass

jar {
    manifest {
        attributes( 'Main-Class': mainClass,
                    'Implementation-Version': version)
    }
}

compileJava {
  options.encoding = 'UTF-8'
}

dependencies {
    compile 'com.google.guava:guava:18.0',
            'com.github.javaparser:javaparser-core:2.2.0',
            'com.beust:jcommander:1.48'

    testCompile 'junit:junit:4.12',
                'org.hamcrest:hamcrest-core:1.3',
                'org.hamcrest:hamcrest-library:1.3',
                'org.mockito:mockito-all:1.10.19'
}

tasks.withType(FindBugs) {
    //ignoreFailures true
    reports {
        xml.enabled = false
        html.enabled = true
    }
 }

jacoco {
    toolVersion = '0.7.5.201505241946'
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = true
    }
}

javadoc {
    failOnError = false
}

task reports {
    group = 'reporting'
    description = 'Generates an uber report about your project.'
    dependsOn(check, projectReport, jacocoTestReport, javadoc, buildDashboard)
}

license {
    header rootProject.file('LICENSE')
    strictCheck true
    exclude "**/*.properties"
    // dryRun true
    // ignoreFailures true

    ext.year = Calendar.getInstance().get(Calendar.YEAR)
    ext.name = 'Jeauty contributors'
    ext.link = 'https://github.com/enr/jeauty'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}
